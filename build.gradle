// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    def getVersionCode = {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim().toInteger()
    }

    def getVersionName = {
        return new Date().format('yy.MM.dd')
    }

    ext.kotlin_version = '1.9.22'
    ext.hilt_version = '2.52'
    ext.cucumber_version = '4.4.+'
    ext.version_name = getVersionName()
    ext.version_code = getVersionCode()
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:4.4.1.3373"
        classpath "com.hiya:jacoco-android:0.2"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.23.6"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: "org.sonarqube"

//sonar configuration
sonarqube {
    properties {
        property "sonar.organization", "mercuriete-github"
        property "sonar.projectKey", "android-musician-tools"
        property "sonar.host.url", "https://sonarcloud.io"

        property "sonar.projectVersion", project.version_name
        property "sonar.sourceEncoding", "UTF-8"

        property "sonar.coverage.jacoco.xmlReportPaths", getRootDir().toString() + "/app/build/jacoco/jacoco.xml"
        property "sonar.junit.reportsPath", getRootDir().toString() + "/app/build/test-results/testDebugUnitTest/"
        property "sonar.androidLint.reportPaths", getRootDir().toString() + "/app/build/reports/lint-results-debug.xml"
        property "sonar.kotlin.detekt.reportPaths", getRootDir().toString() + "/app/build/reports/detekt/debug.xml"
    }
}
